buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
        jcenter()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:5.1.73'
        classpath "org.apache.maven.plugins:maven-resources-plugin:3.2.0"
        classpath 'gradle.plugin.com.github.johnrengelman:shadow:7.1.2'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.github.johnrengelman.shadow'

var dev = "dev"

if (project.hasProperty("dev")) {
    version += "-" + dev
}

group = "magmaout.mappet"
archivesBaseName = "mappet"
sourceCompatibility = targetCompatibility = "1.8"
compileJava.options.encoding = 'UTF-8'

minecraft {

    mappings channel: 'snapshot', version: '20171003-1.12'

    accessTransformer = file('src/main/resources/META-INF/mappet_at.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            jvmArgs("-Xmx1G")
        }

        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            jvmArgs("-Xmx1G")
        }
    }
}


dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

    compileOnly files("run/mods/blockbuster-dev.jar")
    compileOnly files("run/mods/metamorph-dev.jar")
    compileOnly files("run/mods/aperture-dev.jar")
    compileOnly files("run/mods/mclib-dev.jar")
    compileOnly files("run/mods/nashorn.jar")
    implementation("org.reflections:reflections:0.10.2")
    implementation("net.minecraftforge:mergetool:0.2.3.3") { force = true }
}
jar {

}

jar.finalizedBy('shadowJar')
jar.finalizedBy('reobfJar')

// BY DYAMO BOLSHOY UMNI MALCHEG
import org.apache.tools.ant.filters.ReplaceTokens
import java.util.stream.Collectors

task processSource(type: Sync) {
    outputs.upToDateWhen { false }

    from sourceSets.main.java
    filter(ReplaceTokens, tokens: [
            MAPPET     : version,
            MCLIB      : mclib,
            METAMORPH  : metamorph,
            BLOCKBUSTER: blockbuster,
            APERTURE: aperture
    ])
    into "$buildDir/src"
}

compileJava {
    source = processSource.outputs
}

shadowJar {
    dependencies {
        include(dependency("org.reflections:reflections:0.10.2"))
        include(dependency("org.slf4j:slf4j-api:1.7.32"))
        include(dependency("org.javassist:javassist:3.28.0-GA"))
    }
    archiveClassifier.set("")

    var dependenciesToRelocate = [
            "org.reflections",
            "org.slf4j",
            "javassist"
    ]

    for (dependency in dependenciesToRelocate) {
        relocate dependency, "magmaout.mappet.libs." + dependency
    }
}

task prepareAssets(type: Copy) {
    group = 'build'

    inputs.property 'version', project.version
    inputs.property "mcversion", mcversion

    project.version += "-" + mcversion

    from (sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand(project.properties)
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    into project.file('build/classes/java/main')
}

classes.dependsOn(prepareAssets)

tasks.withType(Jar).all { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

artifacts {
    archives deobfJar
    archives sourcesJar
}

task scriptingJavadocs(type: Javadoc) {
    source = sourceSets.main.allJava

    include "magmaout/mappet/api/ui/components/**"
    include "magmaout/mappet/client/gui/utils/graphics/Graphic**"
    include "magmaout/mappet/api/scripts/user/**"
}

task scriptingJavadocsJson(type: Exec) {
    executable "javadoc"
    args "-doclet", "mchorse.utils.doclets.JsonDoclet", "-docletpath", "./src/main/resources/assets/mappet/jsonDoclet.jar", "-sourcepath", "./src/main/java/", "-subpackages", "magmaout.mappet.api.scripts.user:magmaout.mappet.api.ui.components:magmaout.mappet.client.gui.utils.graphics"

    doLast {
        file("src/main/resources/assets/mappet/docs.json").setText(file("docs.json").text)
    }
}

tasks.processResources.dependsOn(scriptingJavadocsJson)

task compareLangFiles {
    doLast {
        def help = file("src/main/resources/assets/${archivesBaseName}/lang/")
        def parse = { File file ->
            def map = new HashMap()
            def splits = file.text.split("\n");

            for (split in splits) {
                def index = split.indexOf("=")

                if (index == -1) {
                    continue
                }

                map.put(split.substring(0, index), true)
            }

            return map
        }

        if (!help.exists()) {
            return
        }

        def en = file("${help.getAbsolutePath()}/en_US.lang")
        def en_map = parse(en)

        for (lang_file in help.listFiles()) {
            if (!lang_file.name.endsWith(".lang") || lang_file.name.startsWith("en_US")) {
                continue
            }

            def lang_map = parse(lang_file)
            def missing = new ArrayList()
            def extra = new ArrayList()

            for (key in en_map.keySet()) {
                if (!lang_map.containsKey(key)) {
                    missing.add(key)
                }
            }

            for (key in lang_map.keySet()) {
                if (!en_map.containsKey(key)) {
                    extra.add(key)
                }
            }

            missing.sort()
            extra.sort()

            print("\n")

            if (!missing.isEmpty() || !extra.isEmpty()) {
                if (!missing.isEmpty()) {
                    print("Language file ${lang_file.getName()} misses following keys:\n")

                    for (key in missing) {
                        print("- ${key}\n")
                    }
                }

                if (!extra.isEmpty()) {
                    print("Language file ${lang_file.getName()} has additional keys:\n")

                    for (key in extra) {
                        print("- ${key}\n")
                    }
                }
            } else {
                print("Language file ${lang_file.getName()} fully matches ${en.getName()}!\n")
            }
        }
    }
}

task copyLangStrings {
    doLast {
        def parse = { File file ->
            def map = new HashMap()
            def splits = file.text.split("\n");

            for (split in splits) {
                def index = split.indexOf("=")

                if (index == -1) {
                    continue
                }

                map.put(split.substring(0, index), split.substring(index + 1))
            }

            return map
        }

        def help = file("src/main/resources/assets/${archivesBaseName}/lang/")
        def en = file("${help.getAbsolutePath()}/en_US.lang")

        for (file in help.listFiles()) {
            if (file.name.endsWith(".lang") && !file.name.startsWith("en_")) {
                def strings = parse(file)
                def splits = en.text.split("\n")

                for (def i = 0; i < splits.length; i++) {
                    def str = splits[i];

                    if (str.contains("=")) {
                        def index = str.indexOf("=")
                        def key = str.substring(0, index)
                        def value = str.substring(index)

                        if (strings.containsKey(key)) {
                            splits[i] = key + "=" + strings.get(key)
                        }
                    }
                }

                file.write(splits.join("\n"))

                print("Wrote to ${file.name}...")
            }
        }

        print("Done!")
    }
}

tasks.register('openBuiltJarsDir', Exec) {
    group = 'build'
    description = 'Opens the build/libs directory in Explorer'

    commandLine "cmd", "/c", "explorer ${projectDir}\\build\\libs"
    ignoreExitValue true
}